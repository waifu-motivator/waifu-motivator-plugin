import java.util.stream.Collectors

def properties(String key) {
    return project.findProperty(key).toString()
}

plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.5.20"
    id "org.jetbrains.intellij" version "1.0"
    id "org.jetbrains.changelog" version "1.1.2"
    id "org.jlleitschuh.gradle.ktlint" version "10.0.0"
    id "io.franzbecker.gradle-lombok" version "4.0.0"
    id "org.kordamp.gradle.markdown" version "2.2.0"
}

apply plugin: "org.jetbrains.changelog"

group pluginGroup
version System.getenv().getOrDefault("VERSION", pluginVersion)

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation "com.googlecode.soundlibs:mp3spi:1.9.5.4"
    implementation "commons-io:commons-io:2.11.0"
    implementation "io.sentry:sentry:5.0.1"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation "org.assertj:assertj-core:3.20.2"
    testImplementation "io.mockk:mockk:1.11.0"
}

configurations {
    // sentry brings in a slf4j that breaks when
    // with the platform slf4j
    implementation.exclude group: "org.slf4j"
}

intellij {
    pluginName.set(properties("pluginName"))
    version.set(properties("platformVersion"))
    type.set(properties("platformType"))
    downloadSources.set(properties("platformDownloadSources").toBoolean())

    plugins.set(Collections.singletonList('io.unthrottled.amii:0.10.2'))

    println "Building for IntelliJ version: ${version}"
}

// Configure gradle-changelog-plugin plugin.
// Read more: https://github.com/JetBrains/gradle-changelog-plugin
changelog {
    version = properties("pluginVersion")
    groups = []
}

markdownToHtml {
    sourceDir file("${project.projectDir}/docs")
    outputDir file("${project.buildDir}/html")
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

tasks.runPluginVerifier {
    ideVersions.set(
        pluginVerifierIdeVersions.split(',').stream()
            .map(String::trim)
            .filter(version -> !version.isEmpty())
            .collect(Collectors.toList())
    )
}

tasks.runIde {
    idePath = properties("idePath")
    if (!idePath.isEmpty()) {
        ideDir.set(file(idePath))
    }
}

tasks.publishPlugin {
    token.set(System.getenv().getOrDefault("PUBLISH_TOKEN", ""))
    channels.set(Collections.singletonList(System.getenv().getOrDefault("PUBLISH_CHANNEL", "")))
}

tasks.patchPluginXml {
    sinceBuild.set(sinceBuildVersion)
    untilBuild.set(untilBuildVersion)

    changeNotes.set(changelog.getUnreleased().toHTML())

    def description = "${project.buildDir}/html/plugin.html"
    if (file(description).exists()) {
        pluginDescription.set(file(description).text)
        println "Successfully patched plugin description."
    }
}

tasks.patchPluginXml.dependsOn("markdownToHtml")
